# instance generator for max clique
# (in fact, this simply generates a random graph)

option randseed 0;

## start from sizes/index sets/params of original problem
param n integer, > 0;
set V := 1..n;
set E within {V,V};

## randomly generate missing index sets/params
let n := 160; # initialize number of vertices
param p := 0.9; # probability of creating edge
let E := {}; # initialize the edge set to empty
for {i in V, j in V : i < j} { # no loops or antiparallel arcs
  if Uniform(0,1) < p then {    
    let E := E union {(i,j)}; # create the edge
  }
}

## print out a .dat file (MIND YOU DON'T OVERWRITE OLD .dat FILES!)
print "# file generated by clique-instgen.run" > rndcliq.dat;
printf "param n := %d;\n", n >> rndcliq.dat;
printf "set E :=" >> rndcliq.dat;
for {(i,j) in E} {
  printf " (%d,%d)", i,j >> rndcliq.dat;
}
printf ";\n" >> rndcliq.dat;
